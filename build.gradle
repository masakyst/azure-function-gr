plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id "java"
    id "application"
    id "groovy"
}


sourceCompatibility = 1.8 
targetCompatibility = 1.8 

jar {
    mainClassName = 'azure.bleullc.Function'
}

defaultTasks 'run'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.microsoft.azure:azure-functions-java-core:1.0.0-beta-2'
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

ext {
    azureOutputDir = "$buildDir/azure-functions/"
}

task copyJar(type: Copy) {
    from "$libsDir/${rootProject.name}-all.jar"
    into azureOutputDir
    rename { String fileName ->
        fileName.replace("${rootProject.name}-all", 'function')
    }
}

copyJar.dependsOn(build)
copyJar.group = 'azure'

task copyFunctionDefs(type: Copy) {
    from "src/main/resources/functions"
    into azureOutputDir
    include('**/function.json')
}
copyFunctionDefs.group = 'azure'

task copyOpenApiDef(type: Copy) {
    from "src/main/resources"
    into azureOutputDir
    include('swagger.*')
}
copyOpenApiDef.group = 'azure'

task cleanAzureFunction(type: Delete) {
    delete azureOutputDir
}
cleanAzureFunction.group = 'azure'

task packageAzureFunction(type: Copy) {
    from rootDir
    into azureOutputDir
    include('host.json', 'local.settings.json')
}
packageAzureFunction.dependsOn(cleanAzureFunction, build, copyJar, copyFunctionDefs, copyOpenApiDef)
//packageAzureFunction.dependsOn(build)
packageAzureFunction.group = 'azure'
